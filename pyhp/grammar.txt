
IGNORE: "<\?php|[ \t\n]";
DECIMAL: "-?0|[1-9][0-9]*";
FLOAT: "-?(0?|[1-9][0-9]*)\.[0-9]+([eE][-+]?[0-9]+)?";
STRING: "'[^']*'|\"[^\"]*\"";
ADD_SYMBOL: "(==)|[-+<]";
STRING_JOIN_SYMBOL: "\.";
VARIABLE: "\$[a-zA-Z_][a-zA-Z0-9_]*";
FUNCTION_NAME: "[a-zA-Z_][a-zA-Z0-9_]*";

main: statement* [EOF];

statement: expr ";"
    | VARIABLE "=" expr ";"
    | "while" "(" expr ")" "{" statement* "}"
    | "if" "(" expr ")" "{" statement* "}"
    | "function" function_name "(" param_list ")" "{" statement* "}"
    | "print" expr ";"
    | "return" expr ";"
    | expr;

param_list: param ("," param)*;
value_list: atom ("," atom)*;

expr: atom ADD_SYMBOL expr
    | atom STRING_JOIN_SYMBOL expr
    | atom
    | function_name "(" value_list ")";

function_name: FUNCTION_NAME;
param: VARIABLE;
atom: DECIMAL | FLOAT | VARIABLE | STRING;
